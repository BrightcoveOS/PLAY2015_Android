// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.2'
        classpath 'de.undercouch:gradle-download-task:0.+'
    }
}

project.ext {
    // The directory that will be used to reference dependent libraries.
    // Modify as needed. Defaulted to users home directory.
    libraryDirectory = "${System.getProperty("user.home")}/libs/"
    imaAndroidSdkJarFileName = "ima-android-sdk-beta9.jar"
}

// Automatically download the Google IMA third party zip file and
// extract the jar to satisfy the dependency.
def downloadIMA() {
    def libsdk = libraryDirectory + imaAndroidSdkJarFileName

    // Conditionally download the Google IMA SDK during
    // configuration.  This step is done once.  The 'clean' task will not
    // remove libraries from /libs/.
    File dep = file(libsdk)
    if (!dep.exists()) {
        apply plugin: 'download-task'

        println "Downloading the Google IMA SDK..."

        download {
            src 'https://dl.google.com/in-stream/google_sdk/android/v3/' + imaAndroidSdkJarFileName
            dest file(libraryDirectory + imaAndroidSdkJarFileName)
        }
    }
}

configure(subprojects.findAll {it.name.startsWith('app')}) {
    apply plugin: 'com.android.application'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'http://repo.brightcove.com/snapshots'
        }
        maven {
            url 'http://repo.brightcove.com/releases'
        }
        maven {
            url System.getenv('ANDROID_HOME') + '/extras/android/m2repository'
        }
        maven {
            url System.getenv('ANDROID_HOME') + '/extras/google/m2repository'
        }

        // This top level file repository will provide/contain third party
        // dependencies which are either downloaded directly (not using a
        // Maven repo) or installed manually for something like VisualOn.
        flatDir {
            dirs libraryDirectory
        }
    }

    dependencies {
        compile "com.brightcove.player:exoplayer:${anpVersion}"
        compile "com.brightcove.player:android-sdk:${anpVersion}"

        downloadIMA()
        compile "com.brightcove.player:android-ima-plugin:${anpVersion}"
        compile files(libraryDirectory + imaAndroidSdkJarFileName)
        compile 'com.google.android.gms:play-services:6.5.87'
    }

    android {
        compileSdkVersion 21
        buildToolsVersion "22.0.1"

        defaultConfig {
            minSdkVersion 16
            targetSdkVersion 21
        }
    }
 }

